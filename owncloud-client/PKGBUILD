# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.

_buildtype='Release'

##############################################################

_name=mirall
pkgname=owncloud-client
pkgver=1.6.0
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=('i686' 'x86_64' 'armv7h')
url='http://owncloud.org/'
license=('GPL2')
depends=('qtkeychain' 'qtwebkit' 'neon')
makedepends=('cmake')
optdepends=('python-sphinx: to compile documentation' 'texlive-bin: to compile documentation')
provides=('mirall' 'ocsync')
conflicts=('mirall-git' 'ocsync')
install=owncloud-client.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("http://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2")
md5sums=('3a61ca43602d1a830d6b42ac0c61f6a5')

if [[ ! ${_buildtype} == 'Release' ]] && [[ ! ${_buildtype} == 'release' ]]; then
  options=('debug')
fi

prepare() {
  if [[ -e ${srcdir}/${_name}-${pkgver}-build ]]; then rm -rf ${srcdir}/${_name}-${pkgver}-build; fi
  mkdir ${srcdir}/${_name}-${pkgver}-build
}

build() {
  cd ${srcdir}/${_name}-${pkgver}-build

  cmake -DBUILD_WITH_QT4=on \
        -DWITH_DOC=on \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        ../${_name}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${_name}-${pkgver}-build
  make DESTDIR=${pkgdir} install
}
