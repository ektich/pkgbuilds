# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is DebugFull to help with debugging.

_buildtype="RelWithDebInfo"

##############################################################

pkgname=tomahawk-git
pkgver=0.5.0.2363.g494e75b
pkgrel=2
epoch=1
pkgdesc="A Music Player App written in C++/Qt - development version"
arch=('i686' 'x86_64')
url="http://tomahawk-player.org/"
license=('GPL3')
depends=('phonon' 'taglib' 'boost' 'clucene' 'libechonest2' 'jreen' 'qtweetlib' 'quazip' 'attica' 'qtwebkit' 'liblastfm' 'qtkeychain')
makedepends=('git' 'cmake')
provides=('tomahawk')
conflicts=('tomahawk')
options=(!strip)
install=tomahawk.install
source=(${pkgname}::'git://github.com/tomahawk-player/tomahawk.git')
md5sums=('SKIP')

# Clean options array to strip pkg if release buildtype is chosen
if [[ ${_buildtype} == "Release" ]] || [[ ${_buildtype} == "release" ]]; then
  options=()
fi

pkgver() {
  cd ${srcdir}/${pkgname}
  git describe --always --tags | sed 's|-|.|g'
}

build() {
  cd ${srcdir}/${pkgname}
  cmake -DBUILD_WITH_QT4=on \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib/tomahawk \
        -DCMAKE_BUILD_TYPE=${_buildtype}
  make
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR=${pkgdir} install
}
