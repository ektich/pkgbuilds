# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is Release.

_buildtype='Release'

##############################################################

pkgname=kdeconnect
pkgver=0.1
pkgrel=1
pkgdesc='KDE Connect - kded and kcm'
url='https://albertvaka.wordpress.com/2013/08/05/introducing-kde-connect/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdelibs' 'qjson')
makedepends=('automoc4' 'cmake')
provides=('kdeconnect')
conflicts=('kdeconnect-git')
install=kdeconnect.install
source=("http://download.kde.org/unstable/kdeconnect/0.1/src/${pkgname}-kde-${pkgver}.tar.xz")
md5sums=('66f5e01ce2c37d927198fc08e0cda27c')

if [[ ! ${_buildtype} == 'Release' ]] && [[ ! ${_buildtype} == 'release' ]]; then
  options=(debug)
fi

prepare() {
  if [[ -e ${pkgname}-kde-${pkgver}-build ]]; then rm -rf ${pkgname}-build; fi
  mkdir ${pkgname}-kde-${pkgver}-build
}

build() {
  cd ${pkgname}-kde-${pkgver}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        ../${pkgname}-kde-${pkgver}
  make
}

package() {
  cd ${pkgname}-kde-${pkgver}-build
  make DESTDIR=${pkgdir} install
}
