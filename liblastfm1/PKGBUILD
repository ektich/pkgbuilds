# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is RelWithDebInfo to help with debugging.

_buildtype="Release"

##############################################################

_pkgname=liblastfm
pkgname=("${_pkgname}1")
pkgver=1.0.0
pkgrel=2
pkgdesc='A Qt C++ library for the Last.fm webservices'
url="https://github.com/eartle/${_pkgname}/"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('qt')
makedepends=('lsb-release')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("https://github.com/eartle/${_pkgname}/zipball/${pkgver}"
        '0001-Set-Content-Type-as-Qt-4.8-changes-default-to-applic.patch')
_ziphash=e5f846a
md5sums=('93d4600aa548df0c062cc36d349fc1c0'
         'df529ca12ad9aff0796233b278a48cfe')

build() {
  if [[ -e ${srcdir}/${_pkgname}-${pkgver}-build ]]; then rm -rf ${srcdir}/${_pkgname}-${pkgver}-build; else  mv ${srcdir}/eartle-${_pkgname}-${_ziphash} ${srcdir}/${_pkgname}-${pkgver}; fi

  patch -Np1 -d ${srcdir}/${_pkgname}-${pkgver} -i ${srcdir}/0001-Set-Content-Type-as-Qt-4.8-changes-default-to-applic.patch

  mkdir ${srcdir}/${_pkgname}-${pkgver}-build
  cd ${srcdir}/${_pkgname}-${pkgver}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib/${_pkgname} \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        ../${_pkgname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}-build
  make DESTDIR=${pkgdir} install
}
